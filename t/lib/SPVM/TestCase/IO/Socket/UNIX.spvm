class TestCase::IO::Socket::UNIX {
  use IO::Socket;
  use IO::Socket::IP;
  use IO::Socket::UNIX;
  use Go::Sync::WaitGroup;
  use Go;
  use Fn;
  use Sys::Socket::Constant as SOCKET;
  use TestUtil;
  
  static method new : int ($sock_path : string) {
    
    # Exceptions
    {
      {
        eval { my $socket = IO::Socket::UNIX->new({Peer => $sock_path, Local => $sock_path});; }
        
        unless ($@) {
          return 0;
        }
      }
    }
    
    return 1;
  }
  
  static method accept : int ($sock_path : string) {
    
    my $test_success_ref = [
      1,
      0,
      0
    ];
    
    my $wg = Go::Sync::WaitGroup->new;
    
    $wg->add(2);
    
    # Run a echo server
    Go->go([$wg : Go::Sync::WaitGroup, $sock_path : string, $test_success_ref : int[]] method : void () {
      
      $test_success_ref->[1] = 1;
      
      TestUtil->run_echo_server_accept_once_unix($sock_path, $test_success_ref);
      
      $wg->done;
    });
    
    # Tests
    Go->go([$wg : Go::Sync::WaitGroup, $test_success_ref : int[], $sock_path : string] method : void () {
      
      $test_success_ref->[2] = 1;
      
      {
        my $socket = IO::Socket::UNIX->new({Peer => $sock_path});
        
        unless ($socket->connected) {
          warn;
          $test_success_ref->[0] = 0;
        }
        
        my $peerpath = $socket->peerpath;
        unless ($peerpath eq $sock_path) {
          warn;
          $test_success_ref->[0] = 0;
        }
        
        my $hostpath = $socket->hostpath;
        unless ($hostpath eq "") {
          warn;
          $test_success_ref->[0] = 0;
        }
        
        my $send_buffer = "abc\n";
        
        $socket->send($send_buffer);
        
        $socket->shutdown(SOCKET->SHUT_WR);
        
        my $string_buffer = StringBuffer->new;
        
        my $recv_buffer = (mutable string)new_string_len 1;
        
        while (1) {
          my $recv_length = $socket->recv($recv_buffer);
          
          if ($recv_length > 0) {
            $string_buffer->push($recv_buffer);
          }
          
          if ($recv_buffer eq "\n") {
            last;
          }
        }
        
        unless ($string_buffer->to_string eq "abc\n") {
          warn;
          $test_success_ref->[0] = 0;
        }
        
      }
      
      $wg->done;
    });
    
    Go->gosched;
    
    $wg->wait;
    
    return Array->equals($test_success_ref, [1, 1, 1]);
  }
}


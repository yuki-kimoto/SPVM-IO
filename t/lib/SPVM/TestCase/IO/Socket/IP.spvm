class TestCase::IO::Socket::IP {
  use IO::Socket;
  use IO::Socket::IP;
  use IO::Socket::UNIX;
  use Go::Sync::WaitGroup;
  use Go;
  use Fn;
  use Sys::Socket::Constant as SOCKET;
  use TestUtil;
  use Sys::OS;
  use Sys::Signal;
  use Sys::Signal::Constant as SIGNAL;
  
  INIT {
    unless (Sys::OS->is_windows) {
      Sys::Signal->signal(SIGNAL->SIGPIPE, Sys::Signal->SIG_IGN);
    }
  }
  
  static method ipv4_new : int ($port : int) {
    
    {
      my $host = "127.0.0.1";
      my $socket = IO::Socket::IP->new({PeerAddr => $host, PeerPort => $port});
      
      unless ($socket is_type IO::Socket::IP) {
        return 0;
      }
      
      unless ($socket->connected) {
        return 0;
      }
      
    }
    
    # Proto
    {
      # IPPROTO_UDP
      {
        my $host = "127.0.0.1";
        my $socket = IO::Socket::IP->new({PeerAddr => $host, PeerPort => $port, Proto => SOCKET->IPPROTO_UDP});
        
        unless ($socket->protocol == SOCKET->IPPROTO_UDP) {
          return 0;
        }
        
        unless ($socket->socktype == SOCKET->SOCK_DGRAM) {
          return 0;
        }
      }
      
    }
    
    # Options
    {
      # ReuseAddr
      {
        my $host = "127.0.0.1";
        my $socket = IO::Socket::IP->new({PeerAddr => $host, PeerPort => $port, ReuseAddr => 1});
        
        my $ret = Sys->getsockopt($socket->fileno, SOCKET->SOL_SOCKET, SOCKET->SO_REUSEADDR);
        
        unless ($ret) {
          return 0;
        }
      }
      
      # ReusePort
      if (Sys::OS->is_windows) {
        warn "[Skip Test]Windows do not support SO_REUSEPORT.";
      }
      else {
        my $host = "127.0.0.1";
        my $socket = IO::Socket::IP->new({PeerAddr => $host, PeerPort => $port, ReusePort => 1});
        
        my $ret = Sys->getsockopt($socket->fileno, SOCKET->SOL_SOCKET, SOCKET->SO_REUSEPORT);
        
        unless ($ret) {
          return 0;
        }
      }
      
      # Broadcast
      {
        my $host = "127.0.0.1";
        my $socket = IO::Socket::IP->new({PeerAddr => $host, PeerPort => $port, Proto => SOCKET->IPPROTO_UDP, Broadcast => 1});
        
        my $ret = Sys->getsockopt($socket->fileno, SOCKET->SOL_SOCKET, SOCKET->SO_BROADCAST);
        
        unless ($ret) {
          return 0;
        }
      }
    }
    
    # Exceptions
    {
      {
        my $host = "127.0.0.1";
        eval { my $socket = IO::Socket::IP->new({PeerAddr => $host, Listen => 1}); }
        
        unless ($@) {
          return 0;
        }
      }
    }
    
    return 1;
  }
  
  static method ipv4_peerhost : int ($port : int) {
    
    {
      my $host = "127.0.0.1";
      my $socket = IO::Socket::IP->new({PeerAddr => $host, PeerPort => $port});
      
      my $peerhost = $socket->peerhost;
      
      unless ($peerhost eq $host) {
        return 0;
      }
    }
    
    return 1;
  }
  
  static method ipv4_peerport : int ($port : int) {
    
    {
      my $host = "127.0.0.1";
      my $socket = IO::Socket::IP->new({PeerAddr => $host, PeerPort => $port});
      
      my $peerport = $socket->peerport;
      
      unless ($peerport == $port) {
        return 0;
      }
    }
    
    return 1;
  }
  
  static method ipv4_sockhost : int ($port : int) {
    
    {
      my $host = "127.0.0.1";
      my $socket = IO::Socket::IP->new({PeerAddr => $host, PeerPort => $port});
      
      my $sockhost = $socket->sockhost;
      
      unless ($sockhost eq $host) {
        return 0;
      }
    }
    
    return 1;
  }
  
  static method ipv4_sockport : int ($port : int) {
    
    {
      my $host = "127.0.0.1";
      my $socket = IO::Socket::IP->new({PeerAddr => $host, PeerPort => $port});
      
      my $sockport = $socket->sockport;
      
      unless ($sockport > 0) {
        return 0;
      }
      
    }
    
    return 1;
  }
  
  static method ipv4_sockaddr : int ($port : int) {
    
    {
      my $addr = "127.0.0.1";
      my $socket = IO::Socket::IP->new({PeerAddr => $addr, PeerPort => $port});
      
      my $sockaddr = $socket->sockaddr;
      
      unless ($sockaddr is_type Sys::Socket::In_addr) {
        return 0;
      }
    }
    
    return 1;
  }
  
  static method ipv4_peeraddr : int ($port : int) {
    
    {
      my $addr = "127.0.0.1";
      my $socket = IO::Socket::IP->new({PeerAddr => $addr, PeerPort => $port});
      
      my $peeraddr = $socket->peeraddr;
      
      unless ($peeraddr is_type Sys::Socket::In_addr) {
        return 0;
      }
    }
    
    return 1;
  }
  
  static method ipv6_new : int ($port : int) {
    
    {
      my $host = "::1";
      my $socket = IO::Socket::IP->new({PeerAddr => $host, PeerPort => $port, Domain => SOCKET->AF_INET6});
      
      unless ($socket is_type IO::Socket::IP) {
        return 0;
      }
      
      unless ($socket->connected) {
        return 0;
      }
    }
    
    # Options
    {
      {
        my $host = "::1";
        my $socket = IO::Socket::IP->new({PeerAddr => $host, PeerPort => $port, Domain => SOCKET->AF_INET6, V6Only => 1});
        
        my $ret = Sys->getsockopt($socket->fileno, SOCKET->IPPROTO_IPV6, SOCKET->IPV6_V6ONLY);
        
        unless ($ret) {
          return 0;
        }
      }
    }
    
    return 1;
  }
  
  static method ipv6_peerhost : int ($port : int) {
    
    {
      my $host = "::1";
      my $socket = IO::Socket::IP->new({PeerAddr => $host, PeerPort => $port, Domain => SOCKET->AF_INET6});
      
      my $peerhost = $socket->peerhost;
      
      unless ($peerhost eq $host) {
        return 0;
      }
    }
    
    return 1;
  }
  
  static method ipv6_peerport : int ($port : int) {
    
    {
      my $host = "::1";
      my $socket = IO::Socket::IP->new({PeerAddr => $host, PeerPort => $port, Domain => SOCKET->AF_INET6});
      
      my $peerport = $socket->peerport;
      
      unless ($peerport == $port) {
        return 0;
      }
    }
    
    return 1;
  }
  
  static method ipv6_sockhost : int ($port : int) {
    
    {
      my $host = "::1";
      my $socket = IO::Socket::IP->new({PeerAddr => $host, PeerPort => $port, Domain => SOCKET->AF_INET6});
      
      my $sockhost = $socket->sockhost;
      
      unless ($sockhost eq $host) {
        return 0;
      }
    }
    
    return 1;
  }
  
  static method ipv6_sockport : int ($port : int) {
    
    {
      my $host = "::1";
      my $socket = IO::Socket::IP->new({PeerAddr => $host, PeerPort => $port, Domain => SOCKET->AF_INET6});
      
      my $sockport = $socket->sockport;
      
      unless ($sockport > 0) {
        return 0;
      }
      
    }
    
    return 1;
  }
  
  static method ipv6_peeraddr : int ($port : int) {
    
    {
      my $host = "::1";
      my $socket = IO::Socket::IP->new({PeerAddr => $host, PeerPort => $port, Domain => SOCKET->AF_INET6});
      
      my $peeraddr = $socket->peeraddr;
      
      unless ($peeraddr is_type Sys::Socket::In6_addr) {
        return 0;
      }
    }
    
    return 1;
  }
  
  static method ipv6_sockaddr : int ($port : int) {
    
    {
      my $host = "::1";
      my $socket = IO::Socket::IP->new({PeerAddr => $host, PeerPort => $port, Domain => SOCKET->AF_INET6});
      
      my $sockaddr = $socket->sockaddr;
      
      unless ($sockaddr is_type Sys::Socket::In6_addr) {
        return 0;
      }
    }
    
    return 1;
  }
  
  static method accept : int ($port : int) {
    
    my $success_ref = [1];
    my $go_server_ref = [0];
    my $go_tests_ref = [0];
    
    Go->go([$go_server_ref : int[], $success_ref : int[], $go_tests_ref : int[], $port : int] method : void () {
      
      my $wg = Go::Sync::WaitGroup->new;
      
      $wg->add(2);
      
      # Run a echo server
      Go->go([$go_server_ref : int[], $wg : Go::Sync::WaitGroup, $port : int] method : void () {
        
        $go_server_ref->[0] = 1;
        
        TestUtil->run_echo_server_accept_once($port);
        
        $wg->done;
      });
      
      # Tests
      Go->go([$go_tests_ref : int[], $wg : Go::Sync::WaitGroup, $success_ref : int[], $port : int] method : void () {
        
        $go_tests_ref->[0] = 1;
        
        {
          my $host = "127.0.0.1";
          my $socket = IO::Socket::IP->new({PeerAddr => $host, PeerPort => $port});
          
          my $send_buffer = "abc\n";
          
          $socket->send($send_buffer);
          
          $socket->shutdown(SOCKET->SHUT_WR);
          
          my $string_buffer = StringBuffer->new;
          
          my $recv_buffer = (mutable string)new_string_len 1;
          
          while (1) {
            
            my $recv_length = $socket->recv($recv_buffer);
            
            if ($recv_length > 0) {
              $string_buffer->push($recv_buffer);
            }
            
            if ($recv_buffer eq "\n") {
              last;
            }
          }
          
          unless ($string_buffer->to_string eq "abc\n") {
            $success_ref->[0] = 0;
          }
        }
        
        $wg->done;
      });
      
      $wg->wait;
    });
    
    Go->gosched;
    
    unless ($go_server_ref->[0]) {
      return 0;
    }
    
    unless ($go_tests_ref->[0]) {
      return 0;
    }
    
    return $success_ref->[0];
  }
  
  static method ipv6_extra : int ($port : int) {
    
    # Basic
    {
      my $host = "::1";
      my $socket = IO::Socket::IP->new({PeerAddr => $host, PeerPort => $port, Domain => SOCKET->AF_INET6});
      
      my $send_buffer = "GET /hello HTTP/1.0\r\nHost: $host\r\n\r\n";
      $socket->send($send_buffer);
      
      $socket->shutdown(SOCKET->SHUT_WR);
      
      my $recv_buffer = (mutable string)new_string_len 1000;
      
      my $recv_length = $socket->recv($recv_buffer);
      
      unless ($recv_length > 0 && $recv_length < 1000) {
        
        warn $recv_length;
        return 0;
      }
      
      Fn->shorten_null_char($recv_buffer);
      
      my $content = $recv_buffer;
      
      unless (Fn->contains($content, "HTTP")) {
        
        warn dump $content;
        return 0;
      }
      
      unless (Fn->contains($content, "hello")) {
        
        warn dump $content;
        return 0;
      }
      
      $socket->close;
    }
    
    # Auto socket close
    {
      my $host = "::1";
      my $socket = IO::Socket::IP->new({PeerAddr => $host, PeerPort => $port, Domain => SOCKET->AF_INET6});
      
      my $send_buffer = "GET /hello HTTP/1.0\r\nHost: $host\r\n\r\n";
      $socket->send($send_buffer);
      
      $socket->shutdown(SOCKET->SHUT_WR);
      
      my $recv_buffer = (mutable string)new_string_len 1000;
      
      my $recv_length = $socket->recv($recv_buffer);
      
      unless ($recv_length > 0 && $recv_length < 1000) {
        
        warn $recv_length;
        return 0;
      }
      
      Fn->shorten_null_char($recv_buffer);
      
      my $content = $recv_buffer;
      
      unless (Fn->contains($content, "hello")) {
        
        warn dump $content;
        return 0;
      }
    }
    
    return 1;
  }
  
  static method accept_timeout_only_field_value : int ($port : int) {
    
    my $success_ref = [1];
    my $go_server_ref = [0];
    my $go_tests_ref = [0];
    
    Go->go([$go_server_ref : int[], $success_ref : int[], $go_tests_ref : int[], $port : int] method : void () {
      my $wg = Go::Sync::WaitGroup->new;
      
      $wg->add(2);
      
      # Run a echo server
      Go->go([$go_server_ref : int[], $wg : Go::Sync::WaitGroup, $success_ref : int[], $port : int] method : void () {
        
        Fn->defer([$wg : Go::Sync::WaitGroup] method : void () {
          $wg->done;
        });
        
        $go_server_ref->[0] = 1;
        
        {
          my $localhost = "127.0.0.1";
          
          my $server_socket = IO::Socket::IP->new({
            LocalAddr => $localhost,
            LocalPort => $port,
            Listen    => 5,
            ReuseAddr => 1,
            Timeout => 1.0,
          });
          
          my $client_socket = (IO::Socket::IP)undef;
          eval { $client_socket = $server_socket->accept; }
          
          if ($@) {
            if ($@ isa Go::Error::IOTimeout) {
              return;
            }
            else {
              die $@;
            }
          }
          
          unless ($client_socket->timeout == 1.0) {
            $success_ref->[0] = 0;
          }
          
          while (1) {
            
            my $data = (mutable string)new_string_len 1;
            
            my $read_length = $client_socket->sysread($data, length $data);
            
            if ($read_length > 0) {
              $client_socket->syswrite($data);
            }
            else {
              last;
            }
          }
        }
        
      });
      
      # Tests
      Go->go([$go_tests_ref : int[], $wg : Go::Sync::WaitGroup, $success_ref : int[], $port : int] method : void () {
        
        $go_tests_ref->[0] = 1;
        
        {
          my $host = "127.0.0.1";
          my $socket = IO::Socket::IP->new({PeerAddr => $host, PeerPort => $port});
          
          my $send_buffer = "abc\n";
          
          $socket->send($send_buffer);
          
          $socket->shutdown(SOCKET->SHUT_WR);
          
          my $string_buffer = StringBuffer->new;
          
          my $recv_buffer = (mutable string)new_string_len 1;
          
          while (1) {
            
            my $recv_length = $socket->recv($recv_buffer);
            
            if ($recv_length > 0) {
              $string_buffer->push($recv_buffer);
            }
            
            if ($recv_buffer eq "\n") {
              last;
            }
          }
          
          unless ($string_buffer->to_string eq "abc\n") {
            $success_ref->[0] = 0;
          }
        }
        
        $wg->done;
      });
      
      $wg->wait;
    });
    
    Go->gosched;
    
    unless ($go_server_ref->[0]) {
      return 0;
    }
    
    unless ($go_tests_ref->[0]) {
      return 0;
    }
    
    return $success_ref->[0];
  }
  
  static method accept_parallel : int ($port : int) {
    
    my $success_ref = [1];
    my $go_server_ref = [0];
    my $go_tests_ref = [0];
    
    Go->go([$go_server_ref : int[], $success_ref : int[], $go_tests_ref : int[], $port : int] method : void () {
      my $wg = Go::Sync::WaitGroup->new;
      
      $wg->add(2);
      
      # Run a echo server
      my $ch_server_stop = Go->make;
      Go->go([$go_server_ref : int[], $wg : Go::Sync::WaitGroup, $success_ref : int[], $port : int, $ch_server_stop : Go::Channel] method : void () {
        
        Fn->defer([$wg : Go::Sync::WaitGroup] method : void () {
          $wg->done;
        });
        
        $go_server_ref->[0] = 1;
        
        {
          my $localhost = "127.0.0.1";
          
          my $server_socket = IO::Socket::IP->new({
            LocalAddr => $localhost,
            LocalPort => $port,
            Listen    => 5,
            ReuseAddr => 1,
            Timeout => 1.0,
          });
          
          my $select_server_stop = Go->new_select;
          $select_server_stop->set_non_blocking(1);
          $select_server_stop->add_read($ch_server_stop);
          
          while (1) {
            my $client_socket = (IO::Socket::IP)undef;
            eval { $client_socket = $server_socket->accept; }
            
            if ($@) {
              if ($@ isa Go::Error::IOTimeout) {
                
                 my $result_server_stop = $select_server_stop->select;
                
                if ($result_server_stop) {
                  last;
                }
                
                next;
              }
              else {
                die $@;
              }
            }
            
            while (1) {
              
              my $data = (mutable string)new_string_len 1;
              
              my $read_length = $client_socket->sysread($data, length $data);
              
              if ($read_length > 0) {
                $client_socket->syswrite($data);
              }
              else {
                last;
              }
            }
          }
        }
        
      });
      
      # Tests
      Go->go([$go_tests_ref : int[], $wg : Go::Sync::WaitGroup, $success_ref : int[], $port : int, $ch_server_stop : Go::Channel] method : void () {
        
        $go_tests_ref->[0] = 1;
        
        for (my $i = 0; $i < 10; $i++) {
          my $host = "127.0.0.1";
          my $socket = IO::Socket::IP->new({PeerAddr => $host, PeerPort => $port});
          
          my $send_buffer = "abc\n";
          
          $socket->send($send_buffer);
          
          $socket->shutdown(SOCKET->SHUT_WR);
          
          my $string_buffer = StringBuffer->new;
          
          my $recv_buffer = (mutable string)new_string_len 1;
          
          while (1) {
            
            my $recv_length = $socket->recv($recv_buffer);
            
            if ($recv_length > 0) {
              $string_buffer->push($recv_buffer);
            }
            
            if ($recv_buffer eq "\n") {
              last;
            }
          }
          
          unless ($string_buffer->to_string eq "abc\n") {
            $success_ref->[0] = 0;
          }
        }
        
        $ch_server_stop->write(1);
        
        $wg->done;
      });
      
      $wg->wait;
    });
    
    Go->gosched;
    
    unless ($go_server_ref->[0]) {
      return 0;
    }
    
    unless ($go_tests_ref->[0]) {
      return 0;
    }
    
    return $success_ref->[0];
  }
  
  static method read_timeout : int ($port : int) {
    
    my $success_ref = [1];
    my $go_server_ref = [0];
    my $go_tests_ref = [0];
    
    Go->go([$go_server_ref : int[], $success_ref : int[], $go_tests_ref : int[], $port : int] method : void () {
      my $wg = Go::Sync::WaitGroup->new;
      
      $wg->add(2);
      
      # Run a echo server
      my $ch_server_stop = Go->make;
      Go->go([$go_server_ref : int[], $wg : Go::Sync::WaitGroup, $success_ref : int[], $port : int, $ch_server_stop : Go::Channel] method : void () {
        
        Fn->defer([$wg : Go::Sync::WaitGroup] method : void () {
          $wg->done;
        });
        
        $go_server_ref->[0] = 1;
        
        {
          my $localhost = "127.0.0.1";
          
          my $server_socket = IO::Socket::IP->new({
            LocalAddr => $localhost,
            LocalPort => $port,
            Listen    => 5,
            ReuseAddr => 1,
            Timeout => 1.0,
          });
          
          my $select_server_stop = Go->new_select;
          $select_server_stop->set_non_blocking(1);
          $select_server_stop->add_read($ch_server_stop);
          
          while (1) {
            my $client_socket = (IO::Socket::IP)undef;
            
            eval { $client_socket = $server_socket->accept; }
            
            if ($@) {
              if ($@ isa Go::Error::IOTimeout) {
                
                my $result_server_stop = $select_server_stop->select;
                
                if ($result_server_stop) {
                  last;
                }
                
                next;
              }
              else {
                die $@;
              }
            }
            
            while (1) {
              
              my $data = (mutable string)new_string_len 1;
              
              Go->sleep_sec(0.5);
              
              my $read_length = 0;
              eval { $read_length = $client_socket->sysread($data, length $data); }
              
              if ($read_length > 0) {
                eval { $client_socket->syswrite($data); }
                
                if ($@) {
                  last;
                }
              }
              else {
                last;
              }
            }
          }
        }
        
      });
      
      # Tests
      Go->go([$go_tests_ref : int[], $wg : Go::Sync::WaitGroup, $success_ref : int[], $port : int, $ch_server_stop : Go::Channel] method : void () {
        
        Fn->defer([$wg : Go::Sync::WaitGroup, $ch_server_stop : Go::Channel] method : void () {
          $ch_server_stop->write(1);
          $wg->done;
        });
        
        {
          my $host = "127.0.0.1";
          my $socket = IO::Socket::IP->new({PeerAddr => $host, PeerPort => $port, Timeout => 0.25});
          
          my $send_buffer = "abc\n";
          
          $socket->send($send_buffer);
          
          $socket->shutdown(SOCKET->SHUT_WR);
          
          my $string_buffer = StringBuffer->new;
          
          my $recv_buffer = (mutable string)new_string_len 1;
          
          eval { $socket->recv($recv_buffer); }
          
          unless ($@ && $@ isa Go::Error::IOTimeout) {
            $success_ref->[0] = 0;
          }
          
          $go_tests_ref->[0] = 1;
        }
      });
      $wg->wait;
    });
    
    Go->gosched;
    
    unless ($go_server_ref->[0]) {
      return 0;
    }
    
    unless ($go_tests_ref->[0]) {
      return 0;
    }
    
    return $success_ref->[0];
  }
  
  static method connect_deadline : int ($port : int) {
    
    my $success_ref = [1];
    
    {
      my $host = "127.0.0.1";
      
      # Use a deadline that has already passed to ensure an immediate timeout.
      my $deadline = Go::Time->now->add(Go::Duration_1l->new_from_sec(-1.0));
      
      my $socket = (IO::Socket::IP)undef;
      eval {
        $socket = IO::Socket::IP->new({
          PeerAddr => $host,
          PeerPort => $port,
          Deadline => $deadline,
        });
      }
      
      if ($@) {
        # Check if the error is Go::Context::Error::DeadlineExceeded.
        unless ($@ isa Go::Context::Error::DeadlineExceeded) {
          warn "Unexpected error: " . $@;
          $success_ref->[0] = 0;
        }
      }
      else {
        # If the socket is created, the deadline check failed.
        warn "Connection unexpectedly succeeded despite an expired deadline.";
        $success_ref->[0] = 0;
        if ($socket) { $socket->close; }
      }
    }
    
    return $success_ref->[0];
  }
  
  static method read_deadline : int ($port : int) {
    
    my $success_ref = [1];
    
    # Server side
    Go->go([$port : int] method : void () {
      eval {
        my $listen_socket = IO::Socket::IP->new({
          LocalAddr => "127.0.0.1",
          LocalPort => $port,
          Listen => 5,
          ReuseAddr => 1,
        });
        
        my $client_socket = $listen_socket->accept;
        
        # Sleep longer than the client's deadline to trigger the timeout.
        Go->sleep(Go::Duration_1l->new_from_sec(1.0));
        
        $client_socket->close;
        $listen_socket->close;
      }
      if ($@) { warn "Server error: " . $@; }
    });
    
    # Client side
    Go->go([$port : int, $success_ref : int[]] method : void () {
      eval {
        my $host = "127.0.0.1";
        
        # Connect to the server
        my $socket = IO::Socket::IP->new({
          PeerAddr => $host,
          PeerPort => $port,
        });
        
        # Set a short deadline (0.5 seconds from now)
        my $deadline = Go::Time->now->add(Go::Duration_1l->new_from_sec(0.5));
        $socket->set_deadline($deadline);
        
        my $buffer = (mutable string)new_string_len 1024;
        
        # This should block and eventually throw DeadlineExceeded because the server sends nothing.
        $socket->sysread($buffer);
        
        # If no exception is thrown, it's a failure.
        warn "sysread unexpectedly succeeded or returned 0 before deadline.";
        $success_ref->[0] = 0;
        $socket->close;
      }
      if ($@) {
        # Check if the error is exactly Go::Context::Error::DeadlineExceeded
        unless ($@ isa Go::Context::Error::DeadlineExceeded) {
          warn "Unexpected error type: " . $@;
          $success_ref->[0] = 0;
        }
      }
    });
    
    # Run the scheduler until all goroutines are finished.
    Go->gosched;
    
    return $success_ref->[0];
  }
  
  static method write_deadline : int ($port : int) {
    
    my $success_ref = [1];
    my $ch_ready = Go->make(1);
    
    # Server side
    Go->go([$port : int, $ch_ready : Go::Channel] method : void () {
      eval {
        my $listen_socket = IO::Socket::IP->new({
          LocalAddr => "127.0.0.1",
          LocalPort => $port,
          Listen => 5,
          ReuseAddr => 1,
        });
        
        $ch_ready->write(1);
        
        # Accept the connection but DO NOT read.
        my $client_socket = $listen_socket->accept;
        
        # Keep the connection alive longer than the client's deadline.
        Go->sleep(Go::Duration_1l->new_from_sec(2.0));
        
        $client_socket->close;
        $listen_socket->close;
      }
      if ($@) { warn "Server error: " . $@; }
    });
    
    # Client side
    Go->go([$port : int, $success_ref : int[], $ch_ready : Go::Channel] method : void () {
      eval {
        $ch_ready->read;
        
        my $host = "127.0.0.1";
        my $socket = IO::Socket::IP->new({
          PeerAddr => $host,
          PeerPort => $port,
        });
        
        # Set a very short deadline to ensure it triggers.
        my $deadline = Go::Time->now->add(Go::Duration_1l->new_from_sec(0.5));
        $socket->set_deadline($deadline);
        
        # Smaller payload, but send many times.
        my $payload = Fn->repeat("a", 8192);
        
        while (1) {
          $socket->syswrite($payload);
        }
      }
      if ($@) {
        unless ($@ isa Go::Context::Error::DeadlineExceeded) {
          warn "Unexpected error: " . $@;
          $success_ref->[0] = 0;
        }
      } else {
        warn "Exited loop without exception";
        $success_ref->[0] = 0;
      }
    });
    
    Go->gosched;
    return $success_ref->[0];
  }
  
}


# Copyright (c) 2023 Yuki Kimoto
# MIT License

class IO::Socket::INET extends IO::Socket::INET::Base {
  
  use Sys::Socket::Constant as SOCKET;
  use Sys::Socket::Sockaddr;
  use Sys::Socket::Sockaddr::In;
  use Sys::Socket::In_addr;
  use Sys::Socket::Util;
  
  # Class Methods
  static method new : IO::Socket::INET ($options : object[] = undef) {
    
    my $self = new IO::Socket::INET;
    
    $options = Fn->merge_options($options, {Domain => SOCKET->AF_INET});
    
    $self->init($options);
    
    return $self;
  }
  
  # Instance Methods
  method new_from_instance : IO::Socket::INET ($options : object[] = undef) {
    return IO::Socket::INET->new($options);
  }
  
  method create_socket_address : Sys::Socket::Sockaddr ($address : string, $port : int) {
    
    # Socket information
    my $socket_address = Sys::Socket::Sockaddr::In->new;
    $socket_address->set_sin_family(SOCKET->AF_INET);
    $socket_address->set_sin_port(Sys::Socket->htons((short)$port));
    
    my $res_ref = new Sys::Socket::AddrinfoLinkedList[1];
    
    my $hints = Sys::Socket::Addrinfo->new;
    $hints->set_ai_family(SOCKET->AF_INET);
    
    Sys::Socket->getaddrinfo($address, undef, $hints, $res_ref);
    
    my $res = $res_ref->[0];
    
    if ($res) {
      my $res_array = $res->to_array;
      $socket_address->set_sin_addr($res_array->[0]->ai_addr->(Sys::Socket::Sockaddr::In)->sin_addr);
    }
    else {
      die "Sys::Socket->getaddrinfo cannot get an address.";
    }
    
    return $socket_address;
  }
  
  method sockaddr : Sys::Socket::In_addr () {
    my $sockaddr_in_sock = (Sys::Socket::Sockaddr::In)$self->sockname;
    
    my $sockaddr = $sockaddr_in_sock->sin_addr;
    
    return $sockaddr;
  }
  
  method sockport : int () {
    my $sockaddr_in_sock = (Sys::Socket::Sockaddr::In)$self->sockname;
    
    my $sockport_network_byte_order = $sockaddr_in_sock->sin_port;
    
    my $sockport = Sys::Socket->ntohs((short)$sockport_network_byte_order);
    
    return $sockport;
  }
  
  method sockhost : string () {
    my $in_addr_sock = $self->sockaddr;
    
    my $sockhost = (string)undef;
    if ($in_addr_sock) {
      $sockhost = Sys::Socket::Util->inet_ntop($self->{Domain}, $in_addr_sock);
    }
    return $sockhost;
  }
  
}

# Copyright (c) 2024 Yuki Kimoto
# MIT License

class IO::Socket::IP extends IO::Socket {
  version_from IO;
  
  use Sys::Socket::Constant as SOCKET;
  use Sys::Socket::Sockaddr;
  use Sys::Socket::In_addr_base;
  
  use IO::Socket::IP::Import::IPv4;
  use IO::Socket::IP::Import::IPv6;
  
  # Fields
  has LocalAddr : protected string;
  
  has LocalPort : protected int;
  
  has PeerAddr : protected string;
  
  has PeerPort : protected int;
  
  has ReuseAddr : protected byte;
  
  has ReusePort : protected byte;
  
  has Broadcast : protected byte;
  
  has V6Only : byte;
  
  # Class Methods
  static method new : IO::Socket::IP ($options : object[] = undef) {
    
    my $self = new IO::Socket::IP;
    
    $self->init($options);
    
    $self->configure;
    
    return $self;
  }
  
  # Instance Methods
  protected method option_names : string[] () {
    
    my $option_names = Array->merge_string(
      $self->SUPER::option_names,
      [
        "PeerAddr",
        "PeerPort",
        "LocalAddr",
        "LocalPort",
        "ReuseAddr",
        "ReusePort",
        "Broadcast",
        "V6Only",
      ]
    );
    
    return $option_names;
  }
  
  protected method init : void ($options : object[] = undef) {
    
    my $options = Hash->new($options);
    
    # Domain option
    my $domain = $options->get_or_default("Domain", SOCKET->AF_INET)->(int);
    
    # Proto option
    my $proto = $options->get_or_default("Proto", SOCKET->IPPROTO_TCP)->(int);
    
    # Type option
    my $type = -1;
    if ($options->exists("Type")) {
      $type = $options->get("Type")->(int);
    }
    else {
      if ($proto == SOCKET->IPPROTO_TCP) {
        $type = SOCKET->SOCK_STREAM;
      }
      elsif ($proto == SOCKET->IPPROTO_UDP) {
        $type = SOCKET->SOCK_DGRAM;
      }
    }
    
    $self->SUPER::init(Fn->merge_options($options->to_array, {Domain => $domain, Proto => $proto, Type => $type}));
    
    # LocalAddr option
    $self->{LocalAddr} = $options->get_or_default("LocalAddr", undef)->(string);
    
    # LocalPort option
    $self->{LocalPort} = $options->get_or_default("LocalPort", 0)->(int);
    
    # PeerAddr option
    $self->{PeerAddr} = $options->get_or_default("PeerAddr", undef)->(string);
    
    # PeerPort option
    $self->{PeerPort} = $options->get_or_default("PeerPort", 0)->(int);
    
    # ReuseAddr option
    $self->{ReuseAddr} = $options->get_or_default("ReuseAddr", 0)->(byte);
    
    # ReusePort option
    $self->{ReusePort} = $options->get_or_default("ReusePort", 0)->(byte);
    
    # Broadcast option
    $self->{Broadcast} = $options->get_or_default("Broadcast", 0)->(byte);
    
    # V6Only option
    if ($options->exists("V6Only")) {
      $self->{V6Only} = $options->{"V6Only"}->(byte);
    }
    
    if ($self->{PeerAddr} && $self->{Listen} > 0) {
      die "PeerAddr option and Listen option cannot be used together.";
    }
    
    if ($self->{PeerAddr}) {
      $self->{SocketCategory} = IO::Socket->SOCKET_CATEGORY_CLIENT;
    }
    elsif ($self->{Listen}) {
      $self->{SocketCategory} = IO::Socket->SOCKET_CATEGORY_SERVER;
    }
    elsif ($self->{FD} >= 0) {
      $self->{SocketCategory} = IO::Socket->SOCKET_CATEGORY_ACCEPTED;
    }
    else {
      die "[Unexpected Error]Unexpected socket category.";
    }
    
  }
  
  protected method configure : void () {
    
    my $reuse_addr = $self->{ReuseAddr};
    
    my $reuse_port = $self->{ReusePort};
    
    my $broadcast = $self->{Broadcast};
    
    my $peer_address = $self->{PeerAddr};
    
    my $peer_port = $self->{PeerPort};
    
    my $local_address = $self->{LocalAddr};
    
    my $local_port = $self->{LocalPort};
    
    my $listen = $self->{Listen};
    
    my $fd = $self->{FD};
    
    unless ($fd >= 0) {
      $self->socket;
    }
    
    if ($reuse_addr) {
      $self->setsockopt(SOCKET->SOL_SOCKET, SOCKET->SO_REUSEADDR, 1);
    }
    
    if ($reuse_port) {
      $self->setsockopt(SOCKET->SOL_SOCKET, SOCKET->SO_REUSEPORT, 1);
    }
    
    if ($broadcast) {
      $self->setsockopt(SOCKET->SOL_SOCKET, SOCKET->SO_BROADCAST, 1);
    }
    
    # Set default TCP settings (Nagle off, Keep-alive on) if it's a TCP socket.
    # This replaces the direct setsockopt(TCP_NODELAY) call.
    my $type = $self->socktype;
    if ($type == SOCKET->SOCK_STREAM) {
      $self->set_default_tcp_settings;
    }
    
    if (exists $self->{V6Only}) {
      $self->setsockopt(SOCKET->IPPROTO_IPV6, SOCKET->IPV6_V6ONLY, !!$self->{V6Only});
    }
    
    my $SocketCategory = $self->{SocketCategory};
    switch ($SocketCategory) {
      case IO::Socket->SOCKET_CATEGORY_CLIENT : {
        my $sockaddr = $self->create_sockaddr($peer_address, $peer_port);
        $self->{Sockaddr} = $sockaddr;
        
        $self->connect;
      }
      case IO::Socket->SOCKET_CATEGORY_SERVER : {
        my $sockaddr = $self->create_sockaddr($local_address, $local_port);
        $self->{Sockaddr} = $sockaddr;
        
        $self->bind;
        
        $self->listen;
      }
    }
    
    # This must be after calling connect method because Mac does not support non-blocking mode to non-connected socket.
    $self->set_blocking(0);
  }
  
  method sockaddr : Sys::Socket::In_addr_base () {
    my $sockaddr = $self->sockname;
    
    my $in_addr_base = (Sys::Socket::In_addr_base)undef;
    
    my $domain = $self->{Domain};
    if ($domain == SOCKET->AF_INET) {
      $in_addr_base = $self->IO::Socket::IP::Import::IPv4::sockaddr;
    }
    elsif ($domain == SOCKET->AF_INET6) {
      $in_addr_base = $self->IO::Socket::IP::Import::IPv6::sockaddr;
    }
    else {
      die "[Unexpected Error]\$domain:$domain.";
    }
    
    return $in_addr_base;
  }
  
  method sockhost : string () {
    
    my $sockhost = (string)undef;
    
    my $domain = $self->{Domain};
    if ($domain == SOCKET->AF_INET) {
      $sockhost = $self->IO::Socket::IP::Import::IPv4::sockhost;
    }
    elsif ($domain == SOCKET->AF_INET6) {
      $sockhost = $self->IO::Socket::IP::Import::IPv6::sockhost;
    }
    else {
      die "[Unexpected Error]\$domain:$domain.";
    }
    
    return $sockhost;
  }
  
  method sockport : int () {
    
    my $sockport = -1;;
    
    my $domain = $self->{Domain};
    if ($domain == SOCKET->AF_INET) {
      $sockport = $self->IO::Socket::IP::Import::IPv4::sockport;
    }
    elsif ($domain == SOCKET->AF_INET6) {
      $sockport = $self->IO::Socket::IP::Import::IPv6::sockport;
    }
    else {
      die "[Unexpected Error]\$domain:$domain.";
    }
    
    return $sockport;
  }
  
  method peeraddr : Sys::Socket::In_addr_base () {
    
    my $in_addr_base = (Sys::Socket::In_addr_base)undef;
    
    my $domain = $self->{Domain};
    if ($domain == SOCKET->AF_INET) {
      $in_addr_base = $self->IO::Socket::IP::Import::IPv4::peeraddr;
    }
    elsif ($domain == SOCKET->AF_INET6) {
      $in_addr_base = $self->IO::Socket::IP::Import::IPv6::peeraddr;
    }
    else {
      die "[Unexpected Error]\$domain:$domain.";
    }
    
    return $in_addr_base;
  }
  
  method peerhost : string () {
    
    my $peerhost = (string)undef;
    
    my $domain = $self->{Domain};
    if ($domain == SOCKET->AF_INET) {
      $peerhost = $self->IO::Socket::IP::Import::IPv4::peerhost;
    }
    elsif ($domain == SOCKET->AF_INET6) {
      $peerhost = $self->IO::Socket::IP::Import::IPv6::peerhost;
    }
    else {
      die "[Unexpected Error]\$domain:$domain.";
    }
    
    return $peerhost;
  }
  
  method peerport : int () {
    
    my $peerport = -1;;
    
    my $domain = $self->{Domain};
    if ($domain == SOCKET->AF_INET) {
      $peerport = $self->IO::Socket::IP::Import::IPv4::peerport;
    }
    elsif ($domain == SOCKET->AF_INET6) {
      $peerport = $self->IO::Socket::IP::Import::IPv6::peerport;
    }
    else {
      die "[Unexpected Error]\$domain:$domain.";
    }
    
    return $peerport;
  }
  
  method accept : IO::Socket::IP ($peer_ref : Sys::Socket::Sockaddr[] = undef) {
    return (IO::Socket::IP)$self->SUPER::accept($peer_ref);
  }
  
  protected method create_sockaddr : Sys::Socket::Sockaddr ($address : string, $port : int) {
    
    my $sockaddr = (Sys::Socket::Sockaddr)undef;
    
    my $domain = $self->{Domain};
    if ($domain == SOCKET->AF_INET) {
      $sockaddr = $self->IO::Socket::IP::Import::IPv4::create_sockaddr($address, $port);
    }
    elsif ($domain == SOCKET->AF_INET6) {
      $sockaddr = $self->IO::Socket::IP::Import::IPv6::create_sockaddr($address, $port);
    }
    else {
      die "[Unexpected Error]\$domain:$domain.";
    }
    
    return $sockaddr;
  }
  
  private method set_default_tcp_settings : void () {
    my $fd = $self->{FD};
    
    # 1. Disable Nagle's algorithm (TCP_NODELAY) as Go does by default.
    my $tcp_nodelay = 1;
    $self->setsockopt(Sys::Socket::Constant->IPPROTO_TCP, Sys::Socket::Constant->TCP_NODELAY, $tcp_nodelay);
    
    # 2. Enable TCP keep-alive with default settings (e.g., 15 seconds)
    # This matches Go's default behavior for net.Dialer.
    my $keepalive_onoff = 1;
    my $keepalive_idle_sec = 15;
    Sys->set_tcp_keepalive($fd, $keepalive_onoff, $keepalive_idle_sec);
  }
  
}

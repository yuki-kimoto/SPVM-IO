# Copyright (c) 2024 Yuki Kimoto
# MIT License

class IO::Socket::INET::Base extends IO::Socket {
  
  use Sys::Socket::Constant as SOCKET;
  use Sys::Socket::Sockaddr;
  
  use IO::Socket::INET::Import::IPv4;
  use IO::Socket::INET::Import::IPv6;
  
  # Fields
  has PeerAddr : protected string;
  has PeerPort : protected int;
  has LocalAddr : protected string;
  has LocalPort : protected int;
  has ReuseAddr : protected int;
  has ReusePort : protected int;
  has Broadcast : protected int;
  has MultiHomed : protected int;

  has peer_address : string;
  has peer_port : int;
  has local_address : string;
  has local_port : int;
  has proto_number : int;
  
=pod TODO

PeerHost    (Synonym for PeerAddr)
LocalHost   (Synonym for LocalAddr)
Reuse       (Deprecated prefer ReuseAddr)

=cut
  
  protected method configure : void ($options : object[] = undef) {
    
    my $options_h = Hash->new($options);
    
    my $sockdomain = $options_h->delete_or_default_int("Domain", -1);
    unless ($sockdomain >= 0) {
      die "The \"Domain\" option must be specified.";
    }
    $self->{Domain} = $sockdomain;
    
    # Listen option
    my $listen_backlog = $options_h->delete_or_default_int("Listen", 5);
    $self->{Listen} = $listen_backlog;
    
    # ReuseAddr option
    my $reuse_addr = $options_h->delete_or_default_int("ReuseAddr", 0);
    if ($reuse_addr) {
      $self->setsockopt(SOCKET->SOL_SOCKET, SOCKET->SO_REUSEADDR, 1);
    }
    
    # ReusePort option
    my $reuse_port = $options_h->delete_or_default_int("ReusePort", 0);
    if ($reuse_port) {
      $self->setsockopt(SOCKET->SOL_SOCKET, SOCKET->SO_REUSEPORT, 1);
    }
    
    # Broadcast option
    my $broadcast = $options_h->delete_or_default_int("Broadcast", 0);
    if ($broadcast) {
      $self->setsockopt(SOCKET->SOL_SOCKET, SOCKET->SO_BROADCAST, 1);
    }
    
    # PeerAddr option
    my $peer_address = $options_h->delete_or_default_string("PeerAddr", undef);
    $self->{peer_address} = $peer_address;
    
    # PeerPort option
    my $peer_port = $options_h->delete_or_default_int("PeerPort", -1);
    $self->{peer_port} = $peer_port;
    
    # LocalAddr option
    my $local_address = $options_h->delete_or_default_string("LocalAddr", undef);
    $self->{local_address} = $local_address;
    
    # LocalPort option
    my $local_port = $options_h->delete_or_default_int("LocalPort", -1);
    $self->{local_port} = $local_port;
    
    # LocalPort option
    my $proto_number = $options_h->delete_or_default_int("Proto", SOCKET->IPPROTO_TCP);
    $self->{proto_number} = $proto_number;
    
    # Type option
    my $type = $options_h->delete_or_default_int("Type", -1);
    unless ($type >= 0) {
      if ($proto_number == SOCKET->IPPROTO_TCP) {
        $type = SOCKET->SOCK_STREAM;
      }
      elsif ($proto_number == SOCKET->IPPROTO_UDP) {
        $type = SOCKET->SOCK_DGRAM;
      }
      elsif ($proto_number == SOCKET->IPPROTO_ICMP) {
        $type = SOCKET->SOCK_RAW;
      }
      $self->{Type} = $type;
    }
    
    my $domain = $self->sockdomain;
    my $socktype = $self->socktype;
    
    $self->socket($domain, $socktype, $proto_number);
    
    if ($local_port >= 0) {
      my $local_socket_address = $self->create_socket_address($local_address, $local_port);
      $self->bind($local_socket_address);
      my $listen = $self->{Listen};
      $self->listen($listen);
    }
    
    if ($peer_address) {
      my $peer_socket_address = $self->create_socket_address($peer_address, $peer_port);
      $self->connect($peer_socket_address);
    }
    
  }
  
  method option_names : string[] () {
    
    my $option_names = Array->merge_string(
      $self->SUPER::option_names,
      [
        "PeerAddr",
        "PeerPort",
        "LocalAddr",
        "LocalPort",
        "ReuseAddr",
        "ReusePort",
        "Broadcast",
        "MultiHomed",
      ]
    );
    
    return $option_names;
  }
  
  protected method init : void ($options : object[] = undef) {
    
    Fn->check_option_names($options, $self->option_names);
    
    $self->SUPER::init($options);
  }
  
  method create_socket_address : Sys::Socket::Sockaddr ($address : string, $port : int) {
    
    my $socket_address = (Sys::Socket::Sockaddr)undef;
    
    my $domain = $self->{Domain};
    if ($domain == SOCKET->AF_INET) {
      $socket_address = $self->IO::Socket::INET::Import::IPv4::create_socket_address($address, $port);
    }
    elsif ($domain == SOCKET->AF_INET6) {
      $socket_address = $self->IO::Socket::INET::Import::IPv6::create_socket_address($address, $port);
    }
    else {
      die "[Unexpected Error]\$domain:$domain.";
    }
    
    return $socket_address;
  }
  
  method sockaddr : Sys::Socket::In_addr_base () {
    my $sockaddr = $self->sockname;
    
    my $in_addr_base = (Sys::Socket::In_addr_base)undef;
    
    my $domain = $self->{Domain};
    if ($domain == SOCKET->AF_INET) {
      $in_addr_base = $self->IO::Socket::INET::Import::IPv4::sockaddr;
    }
    elsif ($domain == SOCKET->AF_INET6) {
      $in_addr_base = $self->IO::Socket::INET::Import::IPv6::sockaddr;
    }
    else {
      die "[Unexpected Error]\$domain:$domain.";
    }
    
    return $in_addr_base;
  }
  
  method sockport : int () {
    
    my $sockport = -1;;
    
    my $domain = $self->{Domain};
    if ($domain == SOCKET->AF_INET) {
      $sockport = $self->IO::Socket::INET::Import::IPv4::sockport;
    }
    elsif ($domain == SOCKET->AF_INET6) {
      $sockport = $self->IO::Socket::INET::Import::IPv6::sockport;
    }
    else {
      die "[Unexpected Error]\$domain:$domain.";
    }
    
    return $sockport;
  }
  
  method sockhost : string () {
    
    my $sockhost = (string)undef;
    
    my $domain = $self->{Domain};
    if ($domain == SOCKET->AF_INET) {
      $sockhost = $self->IO::Socket::INET::Import::IPv4::sockhost;
    }
    elsif ($domain == SOCKET->AF_INET6) {
      $sockhost = $self->IO::Socket::INET::Import::IPv6::sockhost;
    }
    else {
      die "[Unexpected Error]\$domain:$domain.";
    }
    
    return $sockhost;
  }
  
  method peeraddr : Sys::Socket::In_addr_base () {
    my $sockaddr = $self->peername;
    
    my $in_addr_base = (Sys::Socket::In_addr_base)undef;
    
    my $domain = $self->{Domain};
    if ($domain == SOCKET->AF_INET) {
      $in_addr_base = $self->IO::Socket::INET::Import::IPv4::peeraddr;
    }
    elsif ($domain == SOCKET->AF_INET6) {
      $in_addr_base = $self->IO::Socket::INET::Import::IPv6::peeraddr;
    }
    else {
      die "[Unexpected Error]\$domain:$domain.";
    }
    
    return $in_addr_base;
  }
  
  method peerport : int () {
    
    my $peerport = -1;;
    
    my $domain = $self->{Domain};
    if ($domain == SOCKET->AF_INET) {
      $peerport = $self->IO::Socket::INET::Import::IPv4::peerport;
    }
    elsif ($domain == SOCKET->AF_INET6) {
      $peerport = $self->IO::Socket::INET::Import::IPv6::peerport;
    }
    else {
      die "[Unexpected Error]\$domain:$domain.";
    }
    
    return $peerport;
  }
  
  method peerhost : string () {
    
    my $peerhost = (string)undef;
    
    my $domain = $self->{Domain};
    if ($domain == SOCKET->AF_INET) {
      $peerhost = $self->IO::Socket::INET::Import::IPv4::peerhost;
    }
    elsif ($domain == SOCKET->AF_INET6) {
      $peerhost = $self->IO::Socket::INET::Import::IPv6::peerhost;
    }
    else {
      die "[Unexpected Error]\$domain:$domain.";
    }
    
    return $peerhost;
  }
  
}

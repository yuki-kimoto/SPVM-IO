class IO::File extends IO::Handle : precompile {
  use Sys::IO;
  use Fn;
  use Array;
  use Sys::IO::FileStream;
  use Sys::Ioctl;
  
  # Fields
  has auto_flush : rw byte;
  has stream : private Sys::IO::FileStream;
  
  # Class methods
  native static method STDERR : int ();
  native static method STDIN : int ();
  native static method STDOUT : int ();
  native static method open : IO::File ($file_name : string, $open_mode : string);
  
  method DESTROY : void () {
    $self->flush;
  }
  
  # Instance methods
  native method flush : void ();
  native method print : void ($string : string);
  native method read : int ($bufer : mutable string);
  native method readline : string ();
  
  method slurp : string ($file : string) {
    
    my $buffer_length = 4096;
    my $buffer = (mutable string)new_string_len $buffer_length;
    my $string = "";
    while (1) {
      my $read_length = $self->read($buffer);
      
      if ($read_length < $buffer_length) {
        $string .= Fn->substr($buffer, 0, $read_length);
        last;
      }
      else {
        $string .= (string)$buffer;
      }
    }
    return $string;
  }
  
  method close : int () {
    my $stream = $self->{stream};
    
    my $status = Sys::IO->fclose($stream);
    
    return $status;
  }
  
  method eof : int () {
    my $stream = $self->{stream};
    return Sys::IO->feof($stream);
  }
  
  method fileno : int () {
    my $stream = $self->{stream};
    return Sys::IO->fileno($stream);
  }
  
  method getc : int () {
    my $stream = $self->{stream};
    return Sys::IO->getc($stream);
  }
  
  method print_v2 : int ($string : string) {
    
    my $stream = $self->{stream};
    
    my $length = length $string;
    
    return Sys::IO->fwrite($string, 1, $length, $stream);
  }
  
  method clearerr : void () {
    
    my $stream = $self->{stream};
    
    Sys::IO->clearerr($stream);
  }
  
  method error : int () {
    my $stream = $self->{stream};
    
    return Sys::IO->ferror($stream);
  }
  
  method flush_v2 : int () {
    my $stream = $self->{stream};
    
    return Sys::IO->fflush($stream);
  }
  
  method truncate : int ($legnth : long) {
    my $fileno = $self->fileno;
    
    return IO::File->ftruncate($fileno, $legnth);
  }
  
  method ioctl : int ($request : int, $request_arg = undef : object of Byte|Short|Int|Long|Float|Double|object) {
    my $fileno = $self->fileno;
    
    return Sys::Ioctl->ioctl($fileno, $request, $request_arg);
  }

  method ungetc : int ($c : int) {
    my $stream = $self->{stream};
    
    return IO::File->native_ungetc($c, $stream);
  }

  method sync : int () {
    my $fileno = $self->fileno;
    
    return IO::File->fsync($fileno);
  }
  
  native static method ftruncate : int ($fd : int, $length : long);

  native static method native_ungetc : int ($c : int, $stream : Sys::IO::FileStream);

  native static method fsync : int ($fd : int);
}

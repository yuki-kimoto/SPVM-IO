# Copyright (c) 2023 Yuki Kimoto
# MIT License

class IO::Handle {
  use Sys;
  use Sys::IO;
  use Sys::IO::Constant as IO;
  use Sys::Ioctl::Constant as IOCTL;
  use Sys::IO::Stat;
  use Fn;
  use Format;
  use Scope::Guard;
  use Hash;
  use Sys::OS;
  
  # Fields
  has fd : protected int;
  has autoflush : rw byte;
  has nonblocking : protected int;
  has input_line_number : long;
  
  # Class Methods
  static method new : IO::Handle ($options : object[]) {
    my $self = new IO::Handle;
    
    $self->init($options);
    
    return $self;
  }
  
  # Instance Methods
  method init : void ($options : object[] = undef) {
    my $options_h = Hash->new($options);
    
    $self->{fd} = -1;
    
    for my $key (@{$options_h->keys}) {
      die "The \"$key\" option is not available";
    }
  }
  
  method input_line_number : long () { die "Not implemented"; }
  
  method set_input_line_number : void ($input_line_number : long) { die "Not implemented"; }
  
  method opened : int () {
    my $opened = $self->fileno > -1;
    
    return $opened;
  }
  
  method fileno : int () {
    return $self->{fd};
  }
  
  method print : int ($string : string) {
    return $self->write($string);
  }
  
  method say : int ($string : string) {
    my $status1 = $self->print($string);
    my $status2 = $self->print("\n");
    my $status = $status1 || $status2;
    
    return $status;
  }
  
  method printf : int ($format : string, $args : object[]) {
    my $formated_string = Format->sprintf($format, $args);
    return $self->print($formated_string);
  }
  
  method printflush : int ($string : string) {
    my $old_autoflush = $self->autoflush;
    
    # Restores the autoflush field at the end of the scope.
    my $scope_guard = Scope::Guard->new([has handle : IO::Handle = $self, has old_autoflush : int = $old_autoflush] method : void () {
      my $handle = $self->{handle};
      my $old_autoflush = $self->{old_autoflush};
      
      $handle->set_autoflush($old_autoflush);
    });
    
    $self->set_autoflush(1);
    $self->print($string);
  }
  
  method ioctl : int ($request : int, $request_arg : object of byte[]|short[]|int[]|long[]|float[]|double[]|object = undef) {
    my $fd = $self->fileno;
    
    my $ioctl_ret =  Sys->ioctl($fd, $request, $request_arg);
    
    return $ioctl_ret;
  }
  
  method fcntl : int ($command : int, $command_arg : object of Int|Sys::IO::Flock|object = undef) {
    my $fd = $self->fileno;
    
    my $fcntl_ret = Sys->fcntl($fd, $command, $command_arg);
    
    return $fcntl_ret;
  }
  
  method blocking : int () {
    return !$self->{nonblocking};
  }
  
  method set_blocking : void ($blocking : int) {
    unless ($blocking >= 0) {
      die "The $blocking must be greater than or equal to 0";
    }
    
    my $fd = $self->fileno;
    
    if (Sys::OS->is_windows) {
      my $flags = [(int)!$blocking];
      $self->ioctl(IOCTL->FIONBIO, $flags);
    }
    else {
      my $newmode = 0;
      if ($blocking == 0) {
        $newmode &= ~IO->O_NDELAY;
        $newmode |= IO->O_NONBLOCK;
      }
      else {
        $newmode &= ~(IO->O_NDELAY|IO->O_NONBLOCK);
      }
      
      $self->fcntl(IO->F_SETFL, $newmode);
    }
    
    $self->{nonblocking} = !$blocking;
  }
  
  method write : int ($string : string, $length : int = -1, $offset : int = 0) { die "Not implemented"; }
  
  method read : int ($string : mutable string, $length : int = -1, $offset : int = 0) { die "Not implemented"; }
  
  method syswrite : int ($string : string, $length : int = -1, $offset : int = 0) {
    my $fd = $self->fileno;
    
    my $write_length = Sys->syswrite($fd, $string, $length, $offset);
  }
  
  method sysread : int ($string : mutable string, $length : int = -1, $offset : int = 0) {
    my $fd = $self->fileno;
    
    return Sys->sysread($fd, $string, $length, $offset);
  }
  
  method getc : int () { die "Not implemented"; }
  
  method close : int () { die "Not implemented"; }
  
  method eof : int () { die "Not implemented"; }
  
  method clearerr : void () { die "Not implemented"; }
  
  method error : int () { die "Not implemented"; }
  
  method flush : int () { die "Not implemented"; }
  
  method ungetc : int ($c : int) { die "Not implemented"; }
  
  method getline : string () { die "Not implemented"; }
  
  method getlines : string () { die "Not implemented"; }
  
  method truncate : int ($legnth : long) {
    my $fd = $self->fileno;
    
    return Sys::IO->ftruncate($fd, $legnth);
  }
  
  method stat : Sys::IO::Stat () {
    my $fd = $self->fileno;
    
    my $stat = Sys->fstat($fd);
    
    return $stat;
  }
  
  method sync : int () {
    my $fd = $self->fileno;
    
    my $status =  Sys::IO->fsync($fd);
    
    my $success = $status == 0;
    
    return $success;
  }
}

# Copyright (c) 2023 Yuki Kimoto
# MIT License

class IO::Handle {
  use Sys;
  use Sys::IO;
  use Sys::IO::Constant as IO;
  use Sys::Ioctl::Constant as IOCTL;
  use Sys::IO::Stat;
  use Fn;
  use Format;
  use Scope::Guard;
  use Hash;
  use Sys::OS;
  
  # Fields
  has fd : protected int;
  has autoflush : rw byte;
  has nonblocking : protected int;
  
  # Class Methods
  static method new : IO::Handle ($options : object[]) {
    my $self = new IO::Handle;
    
    $self->init($options);
    
    return $self;
  }
  
  # Instance Methods
  method init : void ($options : object[] = undef) {
    
    $self->{fd} = -1;
    
  }
  
  method opened : int () {
    my $opened = $self->fileno >= 0;
    
    return $opened;
  }
  
  method fileno : int () {
    return $self->{fd};
  }
  
  method blocking : int () {
    return !$self->{nonblocking};
  }
  
  method print : void ($string : string) {
    $self->write($string);
  }
  
  method printf : void ($format : string, $args : object[]) {
    my $formated_string = Format->sprintf($format, $args);
    $self->print($formated_string);
  }
  
  method say : void ($string : string) {
    $self->print($string);
    $self->print("\n");
  }
  
  method ioctl : int ($request : int, $request_arg : object of byte[]|short[]|int[]|long[]|float[]|double[]|object = undef) {
    my $fd = $self->fileno;
    
    my $ioctl_ret =  Sys->ioctl($fd, $request, $request_arg);
    
    return $ioctl_ret;
  }
  
  method fcntl : int ($command : int, $command_arg : object of Int|Sys::IO::Flock|object = undef) {
    my $fd = $self->fileno;
    
    my $fcntl_ret = Sys->fcntl($fd, $command, $command_arg);
    
    return $fcntl_ret;
  }
  
  method set_blocking : void ($blocking : int) {
    
    my $current_blocking = !$self->{nonblocking};
    
    unless (!!$blocking == $current_blocking) {
      
      my $fd = $self->fileno;
      
      if (Sys::OS->is_windows) {
        my $flags = [(int)!$blocking];
        $self->ioctl(IOCTL->FIONBIO, $flags);
      }
      else {
        my $newmode = $self->fcntl(IO->F_GETFL);
        
        if ($blocking == 0) {
          $newmode &= ~IO->O_NDELAY;
          $newmode |= IO->O_NONBLOCK;
        }
        else {
          $newmode &= ~(IO->O_NDELAY|IO->O_NONBLOCK);
        }
        
        $self->fcntl(IO->F_SETFL, $newmode);
      }
      
      $self->{nonblocking} = !$blocking;
    }
  }
  
  method syswrite : int ($string : string, $length : int = -1, $offset : int = 0) {
    my $fd = $self->fileno;
    
    my $write_length = Sys->syswrite($fd, $string, $length, $offset);
  }
  
  method sysread : int ($string : mutable string, $length : int = -1, $offset : int = 0) {
    my $fd = $self->fileno;
    
    return Sys->sysread($fd, $string, $length, $offset);
  }
  
  method truncate : void ($legnth : long) {
    my $fd = $self->fileno;
    
    Sys::IO->ftruncate($fd, $legnth);
  }
  
  method stat : Sys::IO::Stat () {
    my $fd = $self->fileno;
    
    my $stat = Sys->fstat($fd);
    
    return $stat;
  }
  
  method sync : void () {
    my $fd = $self->fileno;
    
    Sys::IO->fsync($fd);
  }
  
  method close : void () { die "Not implemented."; }
  
  method write : int ($string : string, $length : int = -1, $offset : int = 0) { die "Not implemented."; }
  
  method read : int ($string : mutable string, $length : int = -1, $offset : int = 0) { die "Not implemented."; }
  
}
